apply plugin: "com.android.library"

android {
    compileSdkVersion 31
    ndkVersion "25.1.8937393"

    defaultConfig {
        targetSdkVersion 30
        minSdkVersion 21

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_TOOLCHAIN=clang++"
                if (project.hasProperty("HC_NOWEBSOCKETS")) {
                    arguments << "-DHC_NOWEBSOCKETS=1"
                }

                // externalNativeBuild normally skips static
                // lib targets. This forces it to build.
                targets "libHttpClient.Android"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            buildStagingDirectory "${project.buildDir.getParentFile().getAbsolutePath()}/.cxx/${project.name}"
        }
    }

    flavorDimensions "ABI"
    productFlavors {
        full {
            dimension "ABI"
            externalNativeBuild {
                cmake {
                    abiFilters "arm64-v8a", "x86_64"
                }
            }
        }
        arm8 {
            dimension "ABI"
            externalNativeBuild {
                cmake {
                    abiFilters "arm64-v8a"
                }
            }
        }
        x86_64 {
            dimension "ABI"
            externalNativeBuild {
                cmake {
                    abiFilters "x86_64"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility=JavaVersion.VERSION_1_7
        targetCompatibility=JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt")
        }
    }
}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:4.9.1"
}
